/*******************************************************************************
 * Copyright (C) 2013-2020 itemis AG (http://www.itemis.eu).
 * 
 * This program and the accompanying materials are made
 * available under the terms of the Eclipse Public License 2.0
 * which is available at https://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *    Alexander Nittka (alex@nittka.de) - initial implementation
 ******************************************************************************/
grammar de.itemis.tooling.terminology.Terminology with org.eclipse.xtext.common.Terminals

generate terminology "http://www.itemis.de/tooling/terminology/Terminology"

TermsFile:Terminology|SubjectEntries;
Terminology:
	"Terminology" name=ID titleInfo=STRING
	"Definitions"
		("Subjects" subjects+=Subject (','subjects+=Subject)*)
		("Status" status+=Status (',' status+=Status)*)
		("Languages" languages+=Language (',' languages+=Language)*)
		("Gr" grs+=Gr (','grs+=Gr)*)
		("Authors" authors+=Author (',' authors+=Author)*)
		("Products" products+=Product(','products+=Product)*)?
		("Customers" customers+=Customer(','customers+=Customer)*)?
;

WithNameAndDisplayName:Status|Subject|Gr|Author|Product|Customer|Language;

Status: name=ID displayName=STRING status=EntryStatus;
Subject:name=ID displayName=STRING;
Language:name=ID (displayName=STRING)?;
Gr:name=ID displayName=STRING pos=Pos gender=Gender? number=GrNumber?;
Author:name=ID displayName=STRING;
Product:name=ID displayName=STRING;
Customer:name=ID displayName=STRING;
Datum hidden(): INT'-'INT'-'INT;
Qname:ID'.'ID;

SubjectEntries:
	"Subject" subject=[Subject|Qname]
		entries+=Entry*
;
Entry:
	('@MissingPreferredTermOK''('
		missingPreferredTermLangage+=[Language](','missingPreferredTermLangage+=[Language])*
	')'
	)?
	"Entry" name=ID '{'
		feedback=Feedback?
		meta=MetaData
		"Definition" definition=STRING
		("Source" definitionSource=STRING)?
		("RelatedEntries" relatedEntries+=[Entry](',' relatedEntries+=[Entry])*)?
		terms+=Term*
	'}'
;
Feedback:
	"@Feedback" text=STRING
;
MetaData:
	"Metadata" "{"
		"Status" status=[Status]
		"created" createdAuthor=[Author] created=Datum
		("modified" modifiedAuthor=[Author] modified=Datum)?
	"}"
;
Term:
	(allowDuplicate?="@DuplicateOK")?
	"Term" name=STRING
		"Status" status=TermStatus
		"Language" language=[Language]
		"Gr" gr=[Gr]
		("Usage" usage=STRING)?
		("Products" products+=[Product](','products+=[Product])*)?
		("Customers" customers+=[Customer](','customers+=[Customer])*)?
;

@Override 
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'\\') | !('\\'|'"') )* '"' 
		; 

enum EntryStatus:unfinished|_OK|consolidated|archive|imported|exported;
enum TermStatus: preferred|permitted|superseded|rejected;
enum Pos:_na|_noun|transitive_verb|intransitive_verb|adjective|adverb;
enum Gender:masculine|feminine|neuter|other;
enum GrNumber:singular|plural;